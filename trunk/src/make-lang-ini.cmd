@echo off

::
:: This script generates the lang.ini file for the installer 
:: to always include the latest translations.
:: Copyright (c) 2010-2012 Stefan Pendl (stefanpe@users.sourceforge.net).
::
:: This program is free software; you can redistribute it and/or modify
:: it under the terms of the GNU General Public License as published by
:: the Free Software Foundation; either version 2 of the License, or
:: (at your option) any later version.
::
:: This program is distributed in the hope that it will be useful,
:: but WITHOUT ANY WARRANTY; without even the implied warranty of
:: MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
:: GNU General Public License for more details.
::
:: You should have received a copy of the GNU General Public License
:: along with this program; if not, write to the Free Software
:: Foundation, Inc., 675 Mass Ave, Cambridge, MA 02139, USA.
::

set LangList=""

for %%L in ( "%~dp0\gui\i18n\*.lng" ) do call :CreateLangString "%%~nL"

call :CreateINI >"%~dp0\installer\lang.ini"

exit /B 0

:CreateLangString
    if %LangList% == "" (
        set LangList="ListItems=%~1"
    ) else (
        set LangList="%LangList:"=%^|%~1"
    )
goto :EOF

:CreateINI
    echo ; Ini file generated by the HM NIS Edit IO designer.
    echo [Settings]
    echo NumFields=3
    echo.
    echo [Field 1]
    echo Type=Label
    echo Text=Select a language pack to be used for UltraDefrag GUI.\r\n\r\nIf your native language is missing here or an existing translation is incomplete, feel free to submit your own translation. Your help will be much appreciated!
    echo Left=1
    echo Right=178
    echo Top=0
    echo Bottom=56
    echo.
    echo [Field 2]
    echo Type=Listbox
    echo Text=Listbox
    echo State=English (US)
    echo %LangList:"=%
    echo Left=182
    echo Right=297
    echo Top=0
    echo Bottom=139
    echo.
    echo [Field 3]
    echo Type=Link
    echo Text=Visit ultradefrag.wikispaces.com for more details...
    echo State=http://ultradefrag.wikispaces.com/
    echo Left=1
    echo Right=163
    echo Top=57
    echo Bottom=72
goto :EOF
